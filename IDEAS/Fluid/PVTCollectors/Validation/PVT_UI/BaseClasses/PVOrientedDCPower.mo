within IDEAS.Fluid.PVTCollectors.Validation.PVT_UI.BaseClasses;
model PVOrientedDCPower
  "Model for a photovoltaic installation which has a power output (DC)"

  parameter Modelica.Units.SI.Irradiance G_STC=1000 "Irradiance at Standard Conditions (usualy 1000 W/m2)"
    annotation (Dialog(group="Characteristics of the photovoltaic panel"));
  parameter Modelica.Units.SI.Power P_STC=370 "Power of one photovoltaic panel at Standard Conditions, usualy equal to power at Maximum Power Point (MPP)"
    annotation (Dialog(group="Characteristics of the photovoltaic panel"));
  parameter Modelica.Units.SI.LinearTemperatureCoefficient gamma=-0.0037 "Temperature coefficient of the photovoltaic panel(s)"
    annotation (Dialog(group="Characteristics of the photovoltaic panel"));
  parameter Modelica.Units.SI.Efficiency P_loss_factor=0.14 "Loss factor of the photovoltaic panel(s)"
    annotation (Dialog(group="Characteristics of the photovoltaic panel"));
  parameter Real n(min=Modelica.Constants.small)=5 "Number of photovoltaic panels in the photovoltaic installation"
    annotation (Dialog(group="Characteristics of the photovoltaic installation"));
  parameter Modelica.Units.SI.Efficiency module_efficiency=0.3 "Module efficiency of the photovoltaic installation";
  parameter Modelica.Units.SI.Angle til=0.785 "Surface tilt of the photovoltaic installation (0°=horizontal, 90°=vertical)"
    annotation (Dialog(group="Characteristics of the photovoltaic installation"));
  parameter Modelica.Units.SI.Angle azi=0 "Surface azimuth (0°=south, 90°=west, 180°=north, 270°=east)"
    annotation (Dialog(group="Characteristics of the photovoltaic installation"));

  Modelica.Units.SI.Temperature T_cell "Cell temperature";
  Modelica.Units.SI.Temperature T_cell_init "Initial cell temperature";
  Modelica.Units.SI.TemperatureDifference T_diff;
  Modelica.Units.SI.Temperature noct_adj "Adjusted nominal operating cell temperature";
  Modelica.Units.SI.Efficiency heat_loss "Heat loss coefficient of the PV panel";
  Modelica.Units.SI.Efficiency wind_loss "Wind loss coefficient of the PV panel";
  Modelica.Units.SI.Velocity wind_speed "Wind speed";
  Modelica.Units.SI.Irradiance G "Total solar irradiance";

  Modelica.Blocks.Math.Add Gglob "Total irradiation on tilted surface"
    annotation (Placement(transformation(
        extent={{10,-10},{-10,10}},
        rotation=90,
        origin={0,10})));

  Modelica.Blocks.Interfaces.RealOutput P(quantity="Power", unit="W") "Power generated by the photovoltaic installation" annotation (Placement(transformation(extent={{-100,
            -10},{-120,10}})));
  Modelica.Blocks.Sources.RealExpression solarPower(y=n*P_STC*G/G_STC*(1+gamma*T_diff)*(1-P_loss_factor))
    "Power generated by the photovoltaic installation" annotation (Placement(transformation(extent={{89,-50},
            {69,-30}})));

  IDEAS.Utilities.IO.SignalExchange.Read reaP(description=
        "Power generated by the PV installation", y(unit="W")) "Read"
    annotation (Placement(transformation(extent={{-20,-50},{-40,-30}})));
protected
  final parameter Modelica.Units.SI.Temperature _T_ref=25+273 "Reference temperature of the cell";
  final parameter Modelica.Units.SI.Temperature _noct=49+273 "Nominal operating cell temperature";
  final parameter Modelica.Units.SI.Velocity wind_init=9.5 "Reference wind speed";

public
  outer Modelica.Blocks.Sources.CombiTimeTable meaDat(
    tableOnFile=true,
    tableName="data",
    fileName=Modelica.Utilities.Files.loadResource("modelica://PvTfluod/Resources/Validation/MeasurementData/Typ1_modelica.txt"),
    columns=1:25) annotation (Placement(transformation(extent={{92,72},{72,92}})));
  Modelica.Blocks.Sources.RealExpression Qdir(y=meaDat.y[2] - meaDat.y[3])
    "[W/m2]"                                                                        annotation (Placement(transformation(extent={{-31.5,
            44},{-12.5,60}})));
  Modelica.Blocks.Sources.RealExpression Qdif(y=meaDat.y[3]) "[W/m2]"
    annotation (Placement(transformation(extent={{-31.5,58},{-12.5,74}})));
  Modelica.Blocks.Sources.RealExpression winSpe(y=meaDat.y[10]) "[m/s]"
    annotation (Placement(transformation(extent={{-79.5,26},{-60.5,42}})));
  Modelica.Blocks.Sources.RealExpression TAmb(y=meaDat.y[12] + 273.15) "[K]"
    annotation (Placement(transformation(extent={{-79.5,12},{-60.5,28}})));
equation
  assert(solarPower.y>=0, "Solar power must be positive");
  noct_adj=_noct+6 "We assume an average distance between the panel and roof of 1.5-2.5 in.";
  T_cell_init=G/800*(noct_adj-293) "in Kelvin";
  heat_loss = 1-module_efficiency/0.9 "We assume a fixed tau alpha of 0.9";
  wind_speed = winSpe.y;
  wind_loss = wind_init/(5.7+3.8*0.51*wind_speed);
  T_cell=TAmb.y+T_cell_init*heat_loss*wind_loss;
  G =Gglob.y;
  T_diff=T_cell-_T_ref;

  connect(solarPower.y, reaP.u)
    annotation (Line(points={{68,-40},{-18,-40}}, color={0,0,127}));
  connect(reaP.y, P) annotation (Line(points={{-41,-40},{-80,-40},{-80,0},{-110,
          0}}, color={0,0,127}));
  connect(Qdir.y, Gglob.u1)
    annotation (Line(points={{-11.55,52},{-6,52},{-6,22}}, color={0,0,127}));
  connect(Qdif.y, Gglob.u2)
    annotation (Line(points={{-11.55,66},{6,66},{6,22}}, color={0,0,127}));
    annotation (Dialog(group="Characteristics of the photovoltaic installation"),
    Icon(coordinateSystem(
        preserveAspectRatio=false,
        extent={{-100,-100},{100,100}},
        grid={1,1}), graphics={
        Line(points={{-100,0},{-59,0}},color={0,0,0}),
        Polygon(
          points={{-80,-52},{-32,63},{78,63},{29,-52},{-80,-52}},
          smooth=Smooth.None,
          fillColor={205,203,203},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None,
          lineColor={0,0,0}),
        Polygon(
          points={{-69,-45},{-57,-19},{-34,-19},{-45,-45},{-69,-45}},
          smooth=Smooth.None,
          fillColor={6,13,150},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Polygon(
          points={{-53,-9},{-41,17},{-18,17},{-29,-9},{-53,-9}},
          smooth=Smooth.None,
          fillColor={6,13,150},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Polygon(
          points={{-38,27},{-26,53},{-3,53},{-14,27},{-38,27}},
          smooth=Smooth.None,
          fillColor={6,13,150},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Polygon(
          points={{-36,-45},{-24,-19},{-1,-19},{-12,-45},{-36,-45}},
          smooth=Smooth.None,
          fillColor={6,13,150},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Polygon(
          points={{-20,-9},{-8,17},{15,17},{4,-9},{-20,-9}},
          smooth=Smooth.None,
          fillColor={6,13,150},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Polygon(
          points={{-5,27},{7,53},{30,53},{19,27},{-5,27}},
          smooth=Smooth.None,
          fillColor={6,13,150},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Polygon(
          points={{-3,-45},{9,-19},{32,-19},{21,-45},{-3,-45}},
          smooth=Smooth.None,
          fillColor={6,13,150},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Polygon(
          points={{13,-9},{25,17},{48,17},{37,-9},{13,-9}},
          smooth=Smooth.None,
          fillColor={6,13,150},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Polygon(
          points={{28,27},{40,53},{63,53},{52,27},{28,27}},
          smooth=Smooth.None,
          fillColor={6,13,150},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Text(
          extent={{-122,35},{-100,9}},
          textColor={0,0,127},
          textString="P")}),
    Diagram(
        coordinateSystem(preserveAspectRatio=false)),
    Documentation(revisions="<html>
<ul>
<li> September 27, 2023, by Javier Arroyo:<br>
Add Read and Overwrite blocks. See <a href=\"https://gitlab.kuleuven.be/positive-energy-districts/moped/-/issues/83\">!83</a>.</li>
<li> August 1, 2023, by Lucas Verleyen:<br>
Add quantity=\"Power\" and unit=\"W\" to RealOutput. (see <a href=\"https://gitlab.kuleuven.be/positive-energy-districts/moped/-/issues/112\">#112</a>)</li>
<li>
December 7, 2022, by Attila Balint and Lucas Verleyen:<br/>
Initial implementation.<br/>
</li>
</ul>
</html>",
        info="<html>
<p>
This is a simple model for a photovoltaic installation. 
The model inputs are wheater data and some parameters set by the user. 
The model outputs the power generated by the PV installation.
</p>
<p>
We use the SAM Photovoltaic Model by NREL: <br/>
Gilman, P., Dobos, A., DiOrio, N., Freeman, J., Janzou, S., Ryberg, D., 
2018, 'SAM Photovoltaic Model Technical Reference Update', 
National Renewable Energy Laboratory Report NREL/TP-6A20-67399.
</p>
</html>"));
end PVOrientedDCPower;
